buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = "${MC_VERSION}-${VERSION}"
group = "com.ewyboy.${MODID}"
archivesBaseName = MODNAME

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    apply plugin: 'idea'
    mappings channel: MCP_CHANNEL, version: MCP_MAPPINGS

    runs {
        client {
            workingDirectory rootProject.file('run')
            property 'fml.earlyprogresswindow', 'false'
            mods {
                barista {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory rootProject.file('run')
            mods {
                barista {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory rootProject.file('run')
            mods {
                barista {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:${MC_VERSION}-${FORGE_VERSION}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    // MixinGradle Settings
    add sourceSets.main, 'mixins.barista.refmap.json'
    config 'mixins.barista.json'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : MODNAME,
                "Specification-Vendor"    : AUTHOR,
                "Specification-Version"   : MC_VERSION,
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : VERSION,
                "Implementation-Vendor"   : AUTHOR,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

curseforge {
    if (project.hasProperty('CF_API_KEY')) {
        apiKey = project.getProperty('CF_API_KEY')
    }
    project {
        id = "${PROJECT_ID}"
        changelogType = 'markdown'
        changelog = file('../CHANGELOG.md')
        releaseType = "${RELEASE_TYPE}"
        addGameVersion "${MOD_LOADER}"
        addGameVersion "${JAVA_VERSION}"
        addGameVersion '1.18.2'
        mainArtifact(jar) {
            displayName = "${MODNAME}-${MC_VERSION_GENERAL}-${VERSION}-${MOD_LOADER}.jar"
        }
    }
    options {
        javaVersionAutoDetect = false
    }
}